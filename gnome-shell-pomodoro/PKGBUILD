# Maintainer: Janne Haapsaari <haaja@iki.fi>
pkgname=gnome-shell-pomodoro
pkgver=0.10.3
pkgrel=2
pkgdesc="This GNOME utility intends to help manage time according to Pomodoro Technique."
arch=('i686' 'x86_64')
url="https://github.com/codito/gnome-shell-pomodoro/"
license=('GPL')
depends=('gnome-desktop'
         'libcanberra'
         'upower'
         'gstreamer')
makedepends=('intltool' 'vala' 'gnome-common')
conflicts=('gnome-shell-extension-pomodoro'
           'gnome-shell-extension-pomodoro-git'
           'gnome-shell-pomodoro-git')
provides=('gnome-shell-pomodoro')
source=("$pkgname"::"https://codeload.github.com/codito/gnome-shell-pomodoro/tar.gz/$pkgver")
md5sums=('a448fd3a2e3aa882df0c8fcbd2ebd69d')
install="gschemas.install"

prepare() {
    cd "$srcdir/$pkgname-$pkgver"
    ./autogen.sh --prefix=/usr --datadir=/usr/share
}

build() {
    cd "$srcdir/$pkgname-$pkgver"
    make
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    make DESTDIR="$pkgdir" install
}
