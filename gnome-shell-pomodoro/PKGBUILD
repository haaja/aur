# Maintainer: Janne Haapsaari <haaja@iki.fi>
pkgname=gnome-shell-pomodoro
pkgver=0.12.3
pkgrel=1
pkgdesc="This GNOME utility intends to help manage time according to Pomodoro Technique."
arch=('i686' 'x86_64')
url="https://github.com/codito/gnome-shell-pomodoro/"
license=('GPL')
depends=('gnome-desktop'
         'libcanberra'
         'upower'
         'gstreamer'
         'gom'
         'gobject-introspection')
makedepends=('intltool' 'vala' 'perl-xml-sax-expat' 'gnome-common')
conflicts=('gnome-shell-extension-pomodoro'
           'gnome-shell-extension-pomodoro-git'
           'gnome-shell-pomodoro-git')
provides=('gnome-shell-pomodoro')
source=("$pkgname"::"https://codeload.github.com/codito/gnome-pomodoro/tar.gz/$pkgver")
md5sums=('30de592c5049d9951f2be4fdf222e265')
install="gschemas.install"

_archive="gnome-pomodoro"

prepare() {
    cd "$srcdir/$_archive-$pkgver"
    ./autogen.sh --prefix=/usr --datadir=/usr/share
}

build() {
    cd "$srcdir/$_archive-$pkgver"
    make
}

package() {
    cd "$srcdir/$_archive-$pkgver"
    make DESTDIR="$pkgdir" install
}
