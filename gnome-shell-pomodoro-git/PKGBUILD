# Maintainer: Janne Haapsaari <haaja@iki.fi>
# Contributor: Christopher Kroo√ü <didi2002 at web.de>
pkgname=gnome-shell-pomodoro-git
pkgver=r426.c2a31b7
pkgrel=2
pkgdesc="This GNOME utility intends to help manage time according to Pomodoro Technique."
arch=('i686' 'x86_64')
url="https://github.com/codito/gnome-shell-pomodoro/"
license=('GPL')
depends=('gnome-desktop'
         'libcanberra'
         'upower'
         'gstreamer')
makedepends=('git' 'intltool' 'vala')
conflicts=('gnome-shell-extension-pomodoro'
           'gnome-shell-extension-pomodoro-git')
provides=('gnome-shell-pomodoro-git')
source=("$pkgname"::'git+https://github.com/codito/gnome-shell-pomodoro.git')
md5sums=('SKIP')
install="gschemas.install"

pkgver() {
    cd "$srcdir/$pkgname"
    printf "r%s.%s" "$(git rev-list --count HEAD)" \
        "$(git rev-parse --short HEAD)"
}

prepare() {
    cd "$srcdir/$pkgname"
    ./autogen.sh --prefix=/usr
}

build() {
    cd "$srcdir/$pkgname"
    make
}

package() {
    cd "$srcdir/$pkgname"
    make DESTDIR="$pkgdir" install
}
